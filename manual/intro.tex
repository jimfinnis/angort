\section{Introduction}
Angort\footnote{The name is an entirely random pair of syllables,
it has no significance.}
is a stack-based concatenative programming language, based on the
venerable Forth, with some
extra features. These include:
\begin{itemize}
\item variables and function parameters
\item higher order functions with (nearly) full lexical closure;
\item lists and hashes (dictionaries) with garbage collection;
\item native C++ plugin architecture.
\end{itemize}
Here are some examples. First, the familiar recursive
quicksort algorithm:
\begin{lstlisting}
# Colon at the start of a line introduces a named function definition.
# This function has one parameter "lst" and one local variable "piv".
:qs |lst:piv| 
    # if length of list is <=1...
    ?lst len 1 <= if
        # return the list itself
        ?lst 
    else 
        # otherwise remove the first item as the pivot
        ?lst pop !piv
        # quicksort the list of items less than the pivot
        ?lst (?piv <) filter qs 
        # add the pivot to that list
        [?piv] + 
        # quicksort the list of items greater than the pivot
        # and add it.
        ?lst (?piv >=) filter qs + 
        # resulting list is left on the stack, and so returned.
    then;
\end{lstlisting}
\clearpage
Next, a program to read a CSV file and print the sums of all
the columns:
\begin{lstlisting}
# load the CSV plugin but do not import the symbols into the
# default namespace, so we have to access them with csv$...
`csv library drop

# load the CSV file - creates a CSV reader object which will read
# into a list of hashes where all columns are floats, and uses it
# to read the file into the global "CSV".

[% `types "f" ] csv$make "magic.log" csv$read !CSV

# make a list of keys from the first item in CSV.
[] ?CSV fst each {i,} !Keys

# create a "slug" (an anonymous function which runs immediately,
# to provide local variables and multiline flow control)
# with a local variable i.
(|:i|
    # for each key, store the iterator value in "i" so it's
    # accessible within a closure
    ?Keys each { i!i
        # create a pair consisting of the iterator (i.e. key name)
        [i, 
         # and the sum of the key's values, done by using map/reduce:
         # the map extracts the key's values, the reduce performs the sum.
         0 ?CSV (?i swap get) map (+) reduce
         ]
        # format the list and print it.
        "%s %f" format.
    }
)@ quit # run the slug and quit
\end{lstlisting}

Angort combines the power and ease of a modern dynamic language with
the convenience of a Forth-like language, and has been used
in various applications including:
\begin{itemize}
\item building command/control/monitoring environments for robotic
systems;
\item scripting experiment runs for neural networks;
\item writing fairly complex data analysis tools;
\item generating visualisations of neural networks and other data;
\item algorithmically generated music;
\item simple 2D games.
\end{itemize}
\clearpage  
Because the interpreter is interactive and upon completion
of a script drops back to a command interface, it is very useful
for building domain-specific control languages. For example, 
our ExoMars locomotion prototype is controlled with Angort, and
its script includes the following definitions:
\begin{lstlisting}
# define a constant "wheels" holding a range from 1-6 inclusive

range 1 7 const wheels

# define a new function "d" with a single parameter "speed"

:d |speed:|
    # set a help text for this function
    :"(speed --) set speed of all drive motors"

    # for each wheel, set the required speed to the value
    # of the parameter
    wheels each {
        ?speed i!drive
    }
;

# slightly more complex function for steering

:t |angle:|
    :"(angle --) turn front wheels one way, back wheels opposite way"
    
    ?angle dup 1!steer 2!steer
    0 0 3!steer 4!steer
    ?angle neg dup 5!steer 6!steer
;

# define a function to stop the rover by setting all speeds to zero
:s 0 d;
\end{lstlisting}
Once these words are defined we can steer the robot in real time with
commands like:
\begin{v}
2500 d
30 t
s
\end{v}
These will set the rover speed to 2500, turn it to 30 degrees, and stop
it respectively. We can also directly type things like:
\begin{v}
wheels each { i dactual .}
\end{v}
which will print the actual speeds of all the wheels.

\subsection{Functional programming}
It's also possible to perform some functional programming with
anonymous functions:
\begin{v}
:sum |list:| 0 ?list (+) reduce;
\end{v}
will allow us to sum a list and print the results:
\begin{v}
[1,2,3,4,5] sum .
\end{v}
or even:
\begin{v}
1 1001 range (dup*) map sum .
\end{v}
to print the sum of the squares of the first 1000 integers.
As can be seen, Angort is a very terse language.

In the examples given so far, functions such as \texttt{dactual},
\texttt{!drive} and \texttt{?drive} are links to native C++ code: it
is very easy to interface Angort with C++.

\subsection{Downloading and building Angort}
Angort can be downloaded from \url{https://github.com/jimfinnis/angort} .
Once downloaded, it can be built and installed 
with the following commands (from
inside the top-level Angort directory):
\begin{v}
mkdir build
cd build
cmake .. -DCMAKE_BUILD_TYPE=Release
make
sudo make install
\end{v}
This is for a Linux machine
with CMake and the \texttt{readline} development libraries. The
interpreter will then be installed, typically as \texttt{/usr/local/bin/angort}. 
A small set of Angort libraries (i.e. libraries written in Angort) will also be installed
into \texttt{/usr/local/share/angort}.

A set
of native C++ plugin libraries can also be downloaded from
\url{https://github.com/jimfinnis/angortplugins}. Once Angort has
been installed, these can be built and installed with
\begin{v}
./buildall
sudo ./install
\end{v}
They will also be installed into \texttt{/usr/local/share/angort}.
Using \texttt{reallybuildall} will attempt to build extra libraries
which require additional packages, such a a CURL interface, SDL
graphics support and JACK MIDI support. Many of these have been written for
somewhat esoteric purposes as I have needed them.


