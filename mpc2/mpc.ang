package mpcc

library "./libmpc.so" drop

none 0 mpc$connect

private



:quote |x:| "\"" ?x "\"" ++;
:doshuffle |lst,n:a,b|
    0 ?n range each {
        rand ?lst len % !a
        rand ?lst len % !b
        ?a ?lst get
        ?b ?lst get
        ?a ?lst set
        ?b ?lst set
    }
    ?lst
;

:dosearch |tag,str:|
    [% ?tag ?str] mpc$search;

public

:artist |name:|
    :"(name -- list) search for songs by an artist"
    `artist ?name dosearch;
    
:album |name:|
    :"(name -- list) search for songs on an album"
    `album ?name dosearch;

:song |name:|
    :"(name -- list) search for songs by title"
    `song ?name dosearch;

:grep |list,str:|
    :"(list str -- list) filter a list of songs for a string"
    ?list (?`name ?str istridx isnone not) filter;

:m
    :"(cmd --) run an mpc$mpc command and list the output"
    mpc$mpc each {i.};

:p
    :"(--) start playing"
    none mpc$play;
:s
    :"(--) stop playing"
    mpc$stop;

:ps
    :"(--) pause"
    mpc$pause;


:l |:stat,list,i,titlen,artlen|
    :"(--) list the current playlist, numbered, indicating the current song"
    mpc$stat !stat
    0!i
    mpc$list !list
    
    # work out some max lengths
    0!titlen
    0!artlen
    ?list each {
        i?`artist len dup ?artlen > if !artlen else drop then
        i?`title  len dup ?titlen > if !titlen else drop then
        
    }
    
    ?titlen 1+ !titlen
    ?artlen 1+ !artlen
    
    {
        ?titlen ?artlen + 100 > if 
            ?titlen 20 > if ?titlen 1- !titlen then
            ?artlen 20 > if ?artlen 1- !artlen then
        else
            leave
        then
    }
    
    ?list each {
        ?i ?stat?`pos = if
            ">> " std$p
        else
            "   " std$p
        then
        
        
        [?i, 
         i?`artist ?artlen truncstr ?artlen padleft,
         i?`title  ?titlen truncstr ?titlen padleft] 
        "%4d %s %s" format .
        ?i 1+ !i
    }
    
;

:stat |:stat,list|
    mpc$stat !stat
    mpc$list !list
    
    "State : "std$p ?stat?`state.
    ?stat?`pos 0 < not if
        ?stat?`pos ?list get dup
        "Current Song: " std$p ?`title.
        "              " std$p ?`artist.
        ?stat?`elapsed std$p "/" std$p ?stat?`total.
    then
    
    
;

:clr mpc$clear;
:add mpc$add;

:d |x:|
    :"(lst --) dump a list or hash"
    ?x type `hash = if
        ?x each {i std$p "   " std$p i ?x get .}
    else
        ?x each {i.}
    then;

:f 
    :"(--) move forwards in current playlist"
    mpc$next;

:b 
    :"(--) move backwards in current playlist"
    mpc$prev;

:go |x:|
    :"(n --) start playing from given song index in playlist"
    ?x mpc$play;

:shuf 
    :"(lst -- lst) shuffle the list"
    dup len doshuffle;

:load
    :"(name --) load a playlist"
    mpc$load;
:save 
    :"(name --) save a playlist"
    mpc$save;

:rm
    :"(name --) delete a playlist"
    mpc$rm;

:ls
    :"() list playlists"
    mpc$playlists each {i.};

endpackage import
