

"../libs/midi" library import
"time" library import
"sdl" library drop

require "queue.ang" import

makeout !O
0 ?O openout

# build the note constants. Sorry we can't say "a#1" because the hash
# is the comment symbol; instead we say "as1".

( |:notes,midi|
    ["c","cs","d","ds","e","f","fs",
     "g","gs","a","as","b"] !notes
    12!midi
    [0,1,2,3,4,5,6,7] each {
        ?notes each {
            ?midi i j + defconst
            ?midi 1+ !midi
        }
    }
)@ 

:n 
    0 ?O on;

:o 
    0 ?O off;

:alloff
    0 127 range each { i o };

:mknotelist |notelist,gap:x|
    "GENERATING.".
    [] ?notelist each { i!x
        [ ?gap 0.1*, (?x 100 n)],
        [ ?gap 0.9*, (?x o)],
    }
;

:gen |notes,interval:|
    (?notes ?interval mknotelist self mkqueue) @
;

[a3,c4,e4,d3] 3 gen
[a3,f4,e4,c4,e4] 4 gen
[a5,g5,d5,e4,f4,f5,e5] 5 gen

now !PrevTime
:loop |:t,t2|
    now!t
    ?t ?PrevTime - process
    ?t !PrevTime
;
        
(dup. "q" asc = if sdl$done then) sdl$onkeydown
(sdl$clear loop sdl$flip) sdl$ondraw
    
300 300 sdl$open

sdl$loop

alloff
0.2 delay
sdl$close 
?O closeout
0!O
quit
