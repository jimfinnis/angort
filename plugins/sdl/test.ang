#../libs/sdl" library import
`sdl library import

-1 !autogc # turn off major GC

:frand # (max -- num) random float
    rand 50000 % 50000.0 / *;

3.1415927 const PI
PI 2 * const TWOPI
# return x,y on the stack - a random vector of length len
:randv |len:| TWOPI frand dup
    cos ?len * 
    swap 
    sin ?len *;

# random colour
:randc |:f| (rand 128 % 128+)!f [?f@,?f@,?f@,255];

:mkthingy |:this,dx,dy| 
    2 frand dup* randv !dy !dx
    [% dup!this
     `x ?Width 2/,
     `y ?Height 2/,
     `dx ?dx,
     `dy ?dy,
     `col randc,
     `move (|:x,y|
         ?this?`x ?this?`dx + !x
         ?this?`y ?this?`dy + !y
         ?x ?Width > ?x 0 < or if
             ?this?`dx neg ?this!`dx
         then
         ?y ?Height > ?y 0 < or if
             ?this?`dy neg ?this!`dy
         then
         ?x ?this!`x
         ?y ?this!`y
     ),
     `draw (
         ?this?`col !col
         ?this?`x ?this?`y 10 10 fillrect
     )
     ]
;


(dup x nl "q" asc = if done then) onkeydown
(
    clear
    ?Thingies each { i?`move@ i?`draw@ }
    flip
) ondraw

#-1 -1 fullscreenopen
"SDLTest" 600 600 open
scrsize 10 - !Width 10 - !Height

[] 0 3000 range each {mkthingy,} !Thingies
loop close quit
