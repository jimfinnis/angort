#"../libs/sdl" library import
`sdl library import

-1 !autogc # turn off major GC

:frand # (max -- num) random float
    rand 50000 % 50000.0 / *;

3.1415927 const PI
PI 2 * const TWOPI

# return x,y on the stack - a random vector of length len
:randv |len:| TWOPI frand dup cos ?len * swap sin ? len *;

# random colour
:randc |:f| (rand 128 % 128+)!f [?f@,?f@,?f@];

:mkthingy |:self,dx,dy| 
    2 frand dup* randv !dy !dx
    [% dup!self
     `x ?Width 2/,
     `y ?Height 2/,
     `dx ?dx,
     `dy ?dy,
     `col randc,
     `move (|:x,y|
         ?self?`x ?self?`dx + !x
         ?self?`y ?self?`dy + !y
         ?x ?Width > ?x 0 < or if
             ?self?`dx neg ?self!`dx
         then
         ?y ?Height > ?y 0 < or if
             ?self?`dy neg ?self!`dy
         then
         ?x ?self!`x
         ?y ?self!`y
     ),
     `draw (
         ?self?`col each{i} col
         ?self?`x ?self?`y 10 10 fillrect
     )
     ]
;


("q" asc = if done then) onkeydown

(
    clear
    ?Thingies each { i?`move@ i?`draw@ }
    flip
) ondraw

-1 -1  fullscreenopen
scr width 10 - !Width 
scr height 10 - !Height

[] 0 3000 range each {mkthingy,} !Thingies
loop close quit
