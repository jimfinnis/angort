#!/usr/local/bin/angort

require "./mpc"

all !All # we'll always need the Big List
"Song list loaded".

# stuff for checking file length / duration ratios.

:makeBigList |:f,len,ct|
    "Generate a list of all songs into alist (y/n)? " std$p
    
    0 1 none io$readstr slice
    dup "y" = swap "Y" = or if
        "alist" "w" io$open !f
        `artist [%] mpc$tags dup sort
        dup len !len
        0!ct
        each {
            ?ct tofloat ?len / 100 * toint "\r" + std$p none io$flush
            ?ct 1+ !ct
            
            i "\n" + ?f io$write
            0 i artist get?`name 48 trunc 50 padleft "\n"+ ?f io$write
        }
        0!f
    then
;


:saveFileLengths |:f|
    # generate and save a hash of name to length
    "lengths" "w" io$open !f
    [%] ?All each {
        i i?`toString@
        i getfn io$stat?`size tofloat,
    }
    ?f io$write;
    

:listRatios |:f,lens,n|
    "lengths" "r" io$open io$readhash !lens
    [] ?All each {
        [%
         `name    i i?`toString@ dup!n,
         ?n ?lens get isnone if
             "Problem: " i?`name + .
             `ratio 0.0
         else
             `ratio   i?`duration tofloat ?n ?lens get /
         then
                  
         ],
    }
    dup (|x,y:| ?x?`ratio ?y?`ratio cmp) fsort
    "ratios" "w" io$open !f
    each {i?`name 50 trunc 50 padright i?`ratio + "\n"+ ?f io$write}
    0!f
;

"duplist" "w" io$open !File

:chkdup |fieldname,compareop:|
    # note that compareop returns 1 if they are the same.
    :"Look for duplicates, given a field name and comparison operator"
    ?All each {
        ?All each {
            i?`name j?`name != if
                ?fieldname i get
                ?fieldname j get ?compareop@ if
                    i?`name " = " j?`name "\n" +++ ?File io$write
                then
            then
            
        }
    }
;

:checkSizeDups
    "Checking for duplicates by size..\n" ?File io$write
    ?All `size (=) chkdup;
    

:checkNameDups
    "Checking for duplicates by name..\n" ?File io$write
    ?All `title (mpc$strneareq) chkdup;


checkNameDups
checkSizeDups

0!File
